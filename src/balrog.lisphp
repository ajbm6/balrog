(use <Silex\Provider\TwigServiceProvider>)

(require "web")
(require "render")
(require "parse")
(require "filesystem")
(require "posts")

(set-param "debug" true)
(register (<Silex\Provider\TwigServiceProvider>)
          (dict ("twig.path" (. base-dir "/layouts"))))

(get "/"
    (lambda [request]
        (render-index-all (get-request-format))))

(get "/index.{_format}"
    (lambda [request]
        (render-index-all (get-request-format))))

(get "/tags/{tag}.{_format}"
    (lambda [request]
        (render-index-tag (get-attribute "tag") (get-request-format))))

(get "/{year}/{month}/{day}/{name}.{_format}"
    (lambda [request]
        (render-post
            (get-post-filename (get-attribute "year")
                               (get-attribute "month")
                               (get-attribute "day")
                               (get-attribute "name"))
            (get-request-format))))

(define (render-post file format)
    (let ([post (parse-post-file file)])
        (render-layout
            (. (at post "layout") "." format ".twig")
            (dict ("post" post)))))

(define (render-index-all format)
    (render-index (list-posts) format))

(define (render-index-tag tag format)
    (render-index (list-posts-by-tag tag) format))

(define (render-index posts format)
    (let ([posts (sort-posts (filter-hidden-posts posts))])
        (render-layout
            (. "index." format ".twig")
            (dict ("posts" posts)
                  ("latest" (get-latest-post posts))))))
